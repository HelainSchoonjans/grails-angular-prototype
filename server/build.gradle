import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.Dockerfile

buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "org.grails.plugins:angular-scaffolding:2.0.0"
        classpath "org.grails.plugins:hibernate5:${gormVersion-".RELEASE"}"
        classpath "org.grails.plugins:views-gradle:1.1.6"

        classpath "com.bmuschko:gradle-docker-plugin:3.6.0"
    }
}

version "0.1.1"
group "grails.angular"

apply plugin:"eclipse"
apply plugin:"idea"
apply plugin:"war"
apply plugin:"org.grails.grails-web"
apply plugin:"org.grails.plugins.views-json"
apply plugin:"com.bmuschko.docker-remote-api"

repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.grails:grails-core"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-tomcat"
    compile "org.grails:grails-plugin-url-mappings"
    compile "org.grails:grails-plugin-rest"
    compile "org.grails:grails-plugin-codecs"
    compile "org.grails:grails-plugin-interceptors"
    compile "org.grails:grails-plugin-services"
    compile "org.grails:grails-plugin-datasource"
    compile "org.grails:grails-plugin-databinding"
    compile "org.grails:grails-web-boot"
    compile "org.grails:grails-logging"
    compile "org.grails.plugins:cache"
    compile "org.grails.plugins:async"
    compile "org.grails.plugins:angular-scaffolding:2.0.0"
    compile "org.grails.plugins:spring-security-rest:2.0.0.RC1"
    compile "org.grails.plugins:hibernate5"
    compile "org.hibernate:hibernate-core:5.1.5.Final"
    compile "org.grails.plugins:views-json"
    compile "org.grails.plugins:views-json-templates"

    // for docker on windows
    compile 'javax.el:javax.el-api:3.0.0'
    compile 'org.glassfish:javax.el:3.0.0'

    console "org.grails:grails-console"
    profile "org.grails.profiles:angular"
    runtime "org.glassfish.web:el-impl:2.1.2-b03"
    runtime "com.h2database:h2"
    runtime "org.apache.tomcat:tomcat-jdbc"
    testCompile "org.grails:grails-gorm-testing-support"
    testCompile "org.grails:grails-datastore-rest-client"
    testCompile "org.grails:grails-web-testing-support"
}

springBoot {
    // Enable the creation of a fully
    // executable archive file.
    // needed to execute our war in docker
    executable = true
}

bootRun {
    jvmArgs('-Dspring.output.ansi.enabled=always')
    addResources = true
    String springProfilesActive = 'spring.profiles.active'
    systemProperty springProfilesActive, System.getProperty(springProfilesActive)
}

// before creating the war, we compile the frontend in the right environment
String env = System.getenv('ENV') ?: System.getProperty('grails.env')
war.baseName = env ? "${env}-ROOT" : 'ROOT'
if(env == 'staging') {
    war.dependsOn(":client:buildStaging")
} else if(env == 'prod' || env == 'production') {
    war.dependsOn(":client:build")
} else {
    war.dependsOn(":client:build")
}

clean {
    delete fileTree(dir: "src/main/webapp")
}
// HSC: you need to enable docker demon running on tls...
ext {
    String name = "grails-angular-prototype"
    if(env == 'prod' || env == 'production' || !env) {
        dockerTag = "heschoon/${name}:latest".toLowerCase()
    } else {
        dockerTag = "heschoon/${name}:${env}.latest".toLowerCase()
    }
    dockerBuildDir = mkdir("${buildDir}/docker")
}

task prepareDocker(type: Copy, dependsOn: assemble) {
    description = 'Copy files from src/main/docker and application jar to Docker temporal build directory'
    group = 'Docker'

    from 'src/main/docker'
    from war.archivePath //copy the war into work folder

    into dockerBuildDir
}

task createDockerfile(type: Dockerfile, dependsOn: prepareDocker) {
    description = 'Create a Dockerfile file'
    group = 'Docker'

    destFile = project.file("${dockerBuildDir}/Dockerfile")

    from 'openjdk:8u151-jdk-alpine'
    label maintainer: 'Helain Schoonjans "helain.schoonjans@outlook.com"'

    exposePort 8080

    workingDir '/app'

    copyFile war.archiveName, 'application.war' //copy the archive into the new docker
    copyFile 'app-entrypoint.sh', 'app-entrypoint.sh'
    runCommand 'chmod +x app-entrypoint.sh'

    entryPoint '/app/app-entrypoint.sh'
}

task buildImage(type: DockerBuildImage, dependsOn: createDockerfile) {
    description = 'Create Docker image to run the Grails application'
    group = 'Docker'

    inputDir = file(dockerBuildDir)
    tag = dockerTag
}